plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    id 'maven-publish'
}

android {
    namespace 'dev.mgks.swv.filehandler'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'dev.mgks.swv'
                artifactId = 'filehandler'
                version = '1.0.0'

                pom {
                    name = 'Android WebView File Handler'
                    description = 'A lightweight library to handle file uploads in Android WebViews.'
                    url = 'https://github.com/mgks/android-webview-file-handler'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'mgks'
                            name = 'Ghazi Khan'
                            email = 'hello@mgks.dev'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/mgks/android-webview-file-handler.git'
                        developerConnection = 'scm:git:ssh://github.com/mgks/android-webview-file-handler.git'
                        url = 'https://github.com/mgks/android-webview-file-handler/tree/main'
                    }
                }
            }
        }

        repositories {
            if (System.getenv("GITHUB_ACTOR") != null) {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/mgks/android-webview-file-handler")

                    credentials {
                        username = System.getenv("GITHUB_ACTOR")
                        password = System.getenv("GITHUB_TOKEN")
                    }
                }
            }
        }
    }
}